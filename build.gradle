// Top-level build file where you can add configuration options common to all sub-projects/modules.
apply plugin: 'org.jetbrains.dokka'

buildscript {
    ext.kotlin_version = '1.7.10'
    ext.kotlinx_coroutines_version = '1.6.4'
    ext.kotlinx_serialization_version = '1.4.0'
    ext.core_ktx_version = '1.9.0'
    ext.hilt_version = '2.43.2'
    ext.dokka_version = '0.10.1'
    ext.seeded_crypto = 'main-SNAPSHOT'
    ext.read_dicekey = 'main-SNAPSHOT'
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.3.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version" // https://kotlinlang.org/docs/gradle.html#dependency-on-the-standard-library
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:2.5.2"
        classpath "com.google.dagger:hilt-android-gradle-plugin:$hilt_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:${dokka_version}"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/dokka"

    // In case of a Gradle multiproject build, you can include subprojects here to get merged documentation
    // Note however, that you have to have the Kotlin plugin available in the root project and in the subprojects
    subProjects = ["seeded", "api"] //, "trustedapp", "dicekey", "read", 

    configuration {
        // Use to include or exclude non public members.
        includeNonPublic = false
        samples = ["api/src/sample"]

        // List of files with module and package documentation
        // https://kotlinlang.org/docs/reference/kotlin-doc.html#module-and-package-documentation
        includes = ['packages.md', 'extra.md'] // , 'packages.md' 'extra.md']

        // Used for linking to JDK documentation
        jdkVersion = 8

        // Allows linking to documentation of the project's dependencies (generated with Javadoc or Dokka)
        // Repeat for multiple links
        externalDocumentationLink {
            // Root URL of the generated documentation to link with. The trailing slash is required!
            url = new URL("https://developer.android.com/reference/androidx/")

            // If package-list file is located in non-standard location
            // packageListUrl = new URL("file:///home/user/localdocs/package-list")
        }
        externalDocumentationLink {
            url = new URL("https://developer.android.com/reference/")
        }
    }
}

task dokkaMarkdown(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'gfm'
    outputDirectory = "docs"
    subProjects = ["seeded", "api"] //, "trustedapp", "dicekey", "read", 

    configuration {
        samples = ["api/src/sample"]
        includeNonPublic = false
        includes = ['packages.md', 'extra.md']
        jdkVersion = 8
        externalDocumentationLink {
            url = new URL("https://developer.android.com/reference/androidx/")
        }
        externalDocumentationLink {
            url = new URL("https://developer.android.com/reference/")
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
